prefix ptp: <http://example.org/def/production-trace-patterns/>

construct {
  ?Event ptp:entity ?EntityLatest .
}
where {
  {
    select ?Event ?Resource (max(?precedingEventTime) as ?precedingEventTime_max)
    where {
      ?Resource a $ResourceType .

      ?Event a $EventType ;
        ptp:entity ?Resource ;
        ptp:atTime ?eventTime .

      ?PrecedingEvent a $PrecedingEventType ;
        ptp:entity ?Resource ;
        ptp:entity ?Entity ;
        ptp:atTime ?precedingEventTime .

      ?Entity a $EntityType .

      filter ( ?precedingEventTime <= ?eventTime )
    }
    group by ?Event ?Resource
  }

  ?PrecedingEvent a $PrecedingEventType ;
    ptp:atTime ?precedingEventTime_max ;
    ptp:entity ?Resource ;
    ptp:entity ?EntityLatest .
  ?EntityLatest a $EntityType .
}