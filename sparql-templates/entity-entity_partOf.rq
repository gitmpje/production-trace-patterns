prefix ptp: <http://example.org/def/production-trace-patterns/>

construct {
  ?PartEntity ptp:isPartOf ?ProductionEntity .
}
where {
  {
    select ?intervalStartTime ?Resource ?ProductionEntity (min(?intervalEndTime_) as ?intervalEndTime)
    where {
      ?ProductionEntity a ptp:ProductionEntity .

      [] a $IntervalStartType ;
        ptp:entity ?Resource ;
        ptp:entity ?ProductionEntity ;
        ptp:atTime ?intervalStartTime .

      [] a $IntervalEndType ;
        ptp:entity ?Resource ;
        ptp:entity ?ProductionEntity ;
        ptp:atTime ?intervalEndTime_ .

      ?Resource a ptp:Resource .

      filter ( ?intervalStartTime <= ?intervalEndTime_ )
    }
    group by ?intervalStartTime ?Resource ?ProductionEntity
  }

  ?Event ptp:entity ?Resource ;
    ptp:entity ?PartEntity ;
    ptp:atTime ?eventTime .

  ?PartEntity a $PartEntityType .

  filter ( ?eventTime >= ?intervalStartTime )
  filter ( ?eventTime <= ?intervalEndTime )
}