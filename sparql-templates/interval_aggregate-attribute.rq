prefix ptp: <http://example.org/def/production-trace-patterns/>

select distinct
  ?intervalStartTime
  ?intervalEndTime
  ?Resource
  ?ProductionEntity
  ?attribute
  (group_concat(str(?value); separator=", ") as ?groupConcat)
  (sum(?value) as ?sum)
  (avg(?value) as ?avg)
  (max(?value) as ?max)
  (min(?value) as ?min)
where {
  {
    select ?intervalStartTime ?Resource ?ProductionEntity (min(?intervalEndTime_) as ?intervalEndTime)
    where {
      #%VALUES%#

      ?ProductionEntity a ptp:ProductionEntity .

      [] a $IntervalStartType ;
        ptp:entity ?Resource ;
        ptp:entity ?ProductionEntity ;
        ptp:atTime ?intervalStartTime .

      [] a $IntervalEndType ;
        ptp:entity ?Resource ;
        ptp:entity ?ProductionEntity ;
        ptp:atTime ?intervalEndTime_ .

      ?Resource a ptp:Resource .

      filter ( ?intervalStartTime <= ?intervalEndTime_ )
    }
    group by ?intervalStartTime ?Resource ?ProductionEntity
  }

  ?Event a $EventType ;
    ptp:entity ?Resource ;
    ptp:atTime ?eventTime ;
    $attribute ?value .

  filter ( ?eventTime >= ?intervalStartTime )
  filter ( ?eventTime <= ?intervalEndTime )
}
group by ?Resource ?ProductionEntity ?intervalStartTime ?intervalEndTime ?attribute